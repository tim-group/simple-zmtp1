plugins {
    id "java"
    id "maven-publish"
    id "com.timgroup.jarmangit" version "1.1.84"
}

ext {
    githubUrl = "https://github.com/tim-group/simple-zmtp1"
}

group = "com.timgroup"
if (System.getenv("BUILD_NUMBER")) version = "1.0.${System.getenv('BUILD_NUMBER')}"

jar {
    manifest { 
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'TIM Group',
            'Implementation-Vendor-Id': project.group,
            'Implementation-URL': githubUrl
        )
    }
}

tasks.withType(JavaCompile).all {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:4.12"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                name "nexus"
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId project.ext.has('artifactId') ? project.ext.artifactId
                    : project == rootProject ? project.name
                    : rootProject.name + project.path.replace(':', '-')
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.ext.has('pomName') ? project.ext.pomName : project.name
                    description = project.description
                    url githubUrl
                    licenses {
                        license {
                            name 'The BSD 2-Clause License'
                            url 'http://opensource.org/licenses/BSD-2-Clause'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            email 'opensource@timgroup.com'
                        }
                    }
                }
            }
        }
    }
}
